# [H] Missing constraint in `default_dict_copy`

## Summary
Severity: High
Contest weight: 0.6340
Dataset id: 22022
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
```solidity
In CairoZero, the correct usage of dict objects created via `default_dict_new` must be paired with a call to `default_dict_finalize` to ensure the integrity and prevent malicious prover’s manipulation of its contents. However, this constraint is missing in the handling of `transient_storage`, `storage` and `valid_jumpdests`, leading to severe vulnerabilities when executing smart contracts.

According to CairoZero’s documentation ([link to `default_dict`](https://docs.cairo-lang.org/reference/common_library.html#default-dict)), a proper workflow involving `default_dict_new` includes a finalization step using `default_dict_finalize`. This ensures the correct initialization of dictionary elements and prevents malicious provers from manipulating dictionary values through hints. Specifically, `default_dict_finalize` enforces the constraint that the initial value of the first element’s `prev_value` in the dictionary must equal to the `default_value`.

However, in the case of `transient_storage`, `storage` and `valid_jumpdests`, this crucial constraint is missing. I will illustrate this issue using `transient_storage`. First, `transient_storage` is initialized in `Account.init()` as follows:
    
    let (transient_storage_start) = default_dict_new(0);

However, there is no subsequent call to `default_dict_finalize(transient_storage_start, transient_storage, 0)` to finalize the storage. Instead, the function `default_dict_copy()` is called on `transient_storage` multiple times during a transaction through the `Account.copy()` function:
    
    let (transient_storage_start, transient_storage) = default_dict_copy(
        self.transient_storage_start, self.transient_storage
    );

This copy operation starts by calling `dict_squash` on the original `transient_storage`:
    
    func default_dict_copy{range_check_ptr}(start: DictAccess*, end: DictAccess*) -> (
        DictAccess*, DictAccess*
    ) {
        alloc_locals;
        let (squashed_start, squashed_end) = dict_squash(start, end);
        local range_check_ptr = range_check_ptr;
        let dict_len = squashed_end - squashed_start;
    
        local default_value;
        if (dict_len == 0) {
            assert default_value = 0;
        } else {
            assert default_value = squashed_start.prev_value;
        }
    
        let (local new_start) = default_dict_new(default_value);
        ...

`dict_squash` itself does not assert the `prev_value` of the first element in the dictionary. As a result, the subsequent copied `transient_storage`’s initial value is also under the malicious prover’s control.
    
        let (local new_start) = default_dict_new(default_value);

If we look at the source code of `default_dict_finalize`, we will notice an extra constraint in the `default_dict_finalize_inner` function:
    
    func default_dict_finalize{range_check_ptr}(
        dict_accesses_start: DictAccess*, dict_accesses_end: DictAccess*, default_value: felt
    ) -> (squashed_dict_start: DictAccess*, squashed_dict_end: DictAccess*) {
        alloc_locals;
        let (local squashed_dict_start, local squashed_dict_end) = dict_squash(
            dict_accesses_start, dict_accesses_end
        );
        local range_check_ptr = range_check_ptr;
    
        default_dict_finalize_inner(
            dict_accesses_start=squashed_dict_start,
            n_accesses=(squashed_dict_end - squashed_dict_start) / DictAccess.SIZE,
            default_value=default_value,
        );
        return (squashed_dict_start=squashed_dict_start, squashed_dict_end=squashed_dict_end);
    }
    
    func default_dict_finalize_inner(
        dict_accesses_start: DictAccess*, n_accesses: felt, default_value: felt
    ) {
        ...
        assert dict_accesses_start.prev_value = default_value;
        ...
    }

As shown above, the additional check besides `dict_squash` is:
    
    assert dict_accesses_start.prev_value = default_value;

This constraint ensures that any uninitialized read from the dictionary returns the correct default value (`0` in this case). However, in `default_dict_copy`, this constraint is absent; meaning the `prev_value` for the first dictionary entry of `transient_storage` is not guaranteed to match the expected default value.

A malicious prover could manipulate the value read from `transient_storage`, `storage` and `valid_jumpdests`. Specifically, they could fabricate a proof where uninitialized keys in the dictionary return values other than the intended default (`0`). This could lead to unintended or unauthorized access to funds, manipulation of contract state, or other security breaches depending on the logic in the upper-level EVM contract.
```

## Recommendation
No recommendation
