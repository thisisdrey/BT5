# [M] Price limit is used as the price

## Summary
Severity: Medium
Contest weight: 0.8106
Dataset id: 23241
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
When initializing a swap on Uniswap V4, the user inputs a price limit that represents the sqrt price at which, if reached, the swap will stop executing, from Uni V4 code.
```solidity
struct SwapParams {
    /// Whether to swap token0 for token1 or vice versa
    bool zeroForOne;
    /// The desired input amount if negative (exactIn), or the desired output amount if positive (exactOut)
    int256 amountSpecified;
    /// The sqrt price at which, if reached, the swap will stop executing
    uint160 sqrtPriceLimitX96;
}
```
When a swap happens in a Uniswap pool, in the swap calculation happens by calling SwapMath.computeSwapStep, where the input price limit is translated to a price target using SwapMath.getSqrtPriceTarget. Knowing that the price target represents the "The price target for the next swap step" https://github.com/Uniswap/v4-core/blob/main/src/libraries/SwapMath.sol#L19
On the other hand, when the internal swap is done in the Uniswap implementation, SwapMath.computeSwapStep is called while passing the price limit as the price target.
```solidity
(, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
    sqrtPriceCurrentX96: sqrtPriceX96,
    sqrtPriceTargetX96: params.sqrtPriceLimitX96,
    liquidity: poolManager.getLiquidity(poolId),
    amountRemaining: int(amountSpecified),
    feePips: 0
});
```
This affects the in/out token calculation, as it will calculate those values based on a wrong target, forcing swap TXs to unexpectedly revert.
When calculating internal swaps, the input price limit is used as the price range for the swap calculation, here and here.
Swap transactions will revert in most cases; DOSing swaps.

## Proof of Concept
Add the following test in flayer/test/UniswapImplementation.t.sol:
```solidity
function test_WrongPriceTargetUsed() public withLiquidity withTokens {
    bool flipped = false;
    PoolKey memory poolKey = _poolKey(flipped);
    CollectionToken token = flipped ? flippedToken : unflippedToken;
    ERC721Mock nft = flipped ? flippedErc : unflippedErc;
    uint256 fees = 10 ether;
    deal(address(token), address(this), fees);
    token.approve(address(uniswapImplementation), type(uint).max);
    uniswapImplementation.depositFees(address(nft), 0, fees);
    // token0 = WETH, token1 = token
    assertEq(address(Currency.unwrap(poolKey.currency0)), address(WETH));
    assertEq(address(Currency.unwrap(poolKey.currency1)), address(token));
    // amount of token out to receive
    uint amountSpecified = 15 ether;
    // Uniswap implementation + pool manager have enough tokens to fulfill the swap
    assertGt(
        token.balanceOf(address(uniswapImplementation)) +
        token.balanceOf(address(uniswapImplementation.poolManager())),
        amountSpecified
    );
    // This contract has more than enough WETH to fulfill the swap
    assertEq(WETH.balanceOf(address(this)), 1000 ether);
    // Swap WETH -> TOKEN
    vm.expectRevert();
    poolSwap.swap(
        poolKey,
        IPoolManager.SwapParams({
            zeroForOne: true,
            amountSpecified: int(amountSpecified),
            sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
        }),
        PoolSwapTest.TestSettings({
            takeClaims: false,
            settleUsingBurn: false
        }),
        ""
    );
}
```

## Recommendation
In UniswapImplementation::beforeSwap, whenever the internal swap is being computed, i.e. by calling SwapMath.computeSwapStep, translate the passed price limit to price range, using SwapMath.getSqrtPriceTarget, by doing something similar to:
```solidity
(, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
    sqrtPriceCurrentX96: sqrtPriceX96,
    sqrtPriceTargetX96: SwapMath.getSqrtPriceTarget(zeroForOne, step.sqrtPriceNextX96, params.sqrtPriceLimitX96),
    liquidity: poolManager.getLiquidity(poolId),
    amountRemaining: int(amountSpecified),
    feePips: 0
});
```
