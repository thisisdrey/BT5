# [H] Ignoring the fee-on-transfer nature of the pod token

## Summary
Severity: High
Contest weight: 0.7857
Dataset id: 11469
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
The LeverageManager.addLeverage function does not check the actual amount of the _pod tokens being received from a user. Since the pod token contract owner can set nonzero transfer fee, the actual received amount might be less than _pTknAmt value, which is used as a parameter for the indexUtils.addLPAndStake invoke. This can cause an insufficient balance error because the indexUtils implementation will try to transfer _pTknAmt from the LeverageManager contract.
```solidity
function addLeverage(
    uint256 _positionId,
    address _pod,
    uint256 _pTknAmt,
    uint256 _pairedLpDesired,
    uint256 _userProvidedDebtAmt,
    address _selfLendingPairPod,
    bytes memory _config
) external override workflow(true) {
    address _sender = _msgSender();
    if (_positionId == 0) {
        _positionId = _initializePosition(_pod, _sender, address(0), _selfLendingPairPod);
    } else {
        address _owner = positionNFT.ownerOf(_positionId);
        require(
            _owner == _sender || positionNFT.getApproved(_positionId) == _sender || positionNFT.isApprovedForAll(_owner, _sender),
            "AUTH"
        );
    }
    _pod = positionProps[_positionId].pod;
    require(_getFlashSource(_positionId) != address(0), "FSV");
    IERC20(_pod).safeTransferFrom(_sender, address(this), _pTknAmt); // @audit pod token is FOT
    if (_userProvidedDebtAmt > 0) {
        IERC20(_getBorrowTknForPod(_positionId)).safeTransferFrom(_sender, address(this), _userProvidedDebtAmt);
    }
    // if additional fees required for flash source, handle that here
    _processExtraFlashLoanPayment(_positionId, _sender);
    IFlashLoanSource(_getFlashSource(_positionId)).flash(
        _getBorrowTknForPod(_positionId),
        _pairedLpDesired - _userProvidedDebtAmt,
        address(this),
        abi.encode(
            LeverageFlashProps({
                method: FlashCallbackMethod.ADD,
                positionId: _positionId,
                user: _sender,
                pTknAmt: _pTknAmt, // @audit pod token is FOT
                pairedLpDesired: _pairedLpDesired,
                config: _config
            })
        )
    );
}
```
```solidity
function _lpAndStakeInPod(
    address _borrowToken,
    uint256 _borrowAmt,
    LeverageFlashProps memory _props
) internal returns (
    uint256 _pTknAmtUsed,
    uint256 _pairedLpUsed,
    uint256 _pairedLpLeftover
) {
    (, uint256 _slippage, uint256 _deadline) = abi.decode(_props.config, (uint256, uint256, uint256));
    (address _pairedLpForPod, uint256 _pairedLpAmt) = _processAndGetPairedTknAndAmt(
        _props.positionId, _borrowToken, _borrowAmt, positionProps[_props.positionId]
    );
    uint256 _podBalBefore = IERC20(positionProps[_props.positionId].pod).balanceOf(address(this));
    uint256 _pairedLpBalBefore = IERC20(_pairedLpForPod).balanceOf(address(this));
    IERC20(positionProps[_props.positionId].pod).safeIncreaseAllowance(address(indexUtils), _props.pTknAmt);
    IERC20(_pairedLpForPod).safeIncreaseAllowance(address(indexUtils), _pairedLpAmt);
    indexUtils.addLPAndStake(
        IDecentralizedIndex(positionProps[_props.positionId].pod),
        _props.pTknAmt, // @audit pod token is FOT
        _pairedLpForPod,
        _pairedLpAmt,
        0, // is not used so can use max slippage
        _slippage,
        _deadline
    );
    _pTknAmtUsed = _podBalBefore - IERC20(positionProps[_props.positionId].pod).balanceOf(address(this));
    _pairedLpUsed = _pairedLpBalBefore - IERC20(_pairedLpForPod).balanceOf(address(this));
    _pairedLpLeftover = _pairedLpBalBefore - _pairedLpUsed;
}
```

## Recommendation
Consider using the actually received amount in the LeverageFlashProps.pTknAmt field.
