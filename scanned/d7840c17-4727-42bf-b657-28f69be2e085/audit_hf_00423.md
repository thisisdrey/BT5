# [H] Lenders and borrowers can not

## Summary
Severity: High
Contest weight: 0.9463
Dataset id: 1835
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
The incorrect logic in function veNFTAerodrome::getDataByReceipt() will cause the lenders and borrowers unable to claim liquidation token after the NFT auction is sold.
• The function DebitaV3Loan::claimCollateralAsNFTLender() allows the lenders to claim the liquidation token after the NFT collateral auction is sold.
• The function DebitaV3Loan::claimCollateralNFTAsBorrower() allows the borrower to claim the liquidation token in case of partial default.
• These two functions above call veNFTAerodrome::getDataByReceipt() to retrieve the liquidation token's decimals to calculate the payment amount.
• These two flows above can be reverted because of an unhandled case in the function veNFTAerodrome::getDataByReceipt(). The mentioned unhandled case is when there is no owner of the receipt token, such that ownerOf(receiptID) reverts because of non-existent token.

```solidity
function getDataByReceipt(
    uint receiptID
) public view returns (receiptInstance memory) {
    veNFT veContract = veNFT(nftAddress);
    veNFTVault vaultContract = veNFTVault(s_ReceiptID_to_Vault[receiptID]);
    uint nftID = vaultContract.attached_NFTID();
    IVotingEscrow.LockedBalance memory _locked = veContract.locked(nftID);
    uint _decimals = ERC20(_underlying).decimals();
    address manager = vaultContract.managerAddress();
    address currentOwnerOfReceipt = ownerOf(receiptID);
    receiptInstance memory receiptData = receiptInstance({
        receiptID: receiptID,
        attachedNFT: nftID,
        lockedAmount: uint(int(_locked.amount)),
        lockedDate: _locked.end,
        decimals: _decimals,
        vault: address(vaultContract),
        underlying: _underlying,
        OwnerIsManager: manager == currentOwnerOfReceipt
    });
    return receiptData;
}

function ownerOf(uint256 tokenId) public view virtual returns (address) {
    return _requireOwned(tokenId);
}

...

function _requireOwned(uint256 tokenId) internal view returns (address) {
    address owner = _ownerOf(tokenId);
    if (owner == address(0)) {
        revert ERC721NonexistentToken(tokenId);
    }
    return owner;
}
```

This state can be reached when the auction buyer withdraws veNFT by calling veNFTVault::withdraw(), which will burn the receipt token.

```solidity
function withdraw() external nonReentrant {
    IERC721 veNFTContract = IERC721(veNFTAddress);
    IReceipt receiptContract = IReceipt(factoryAddress);
    uint m_idFromNFT = attached_NFTID;
    address holder = receiptContract.ownerOf(receiptID);
    // RECEIPT HAS TO BE ON OWNER WALLET
    require(attached_NFTID != 0, "No attached nft");
    require(holder == msg.sender, "Not Holding");
    receiptContract.decrease(managerAddress, m_idFromNFT);
    delete attached_NFTID;
    // First: burn receipt
    IReceipt(factoryAddress).burnReceipt(receiptID);
    IReceipt(factoryAddress).emitWithdrawn(address(this), m_idFromNFT);
    // Second: send them their NFT
    veNFTContract.transferFrom(address(this), msg.sender, m_idFromNFT);
}

function burnReceipt(uint id) external onlyVault {
    _burn(id);
}
```

Internal pre-conditions
External pre-conditions
Attack Path
• A borrower deposits veNFT to veNFTVault by calling veNFTAerodrome::deposit(), effectively receives a Receipt token
• The borrower creates borrow offer with the Receipt token as collateral
• The borrow offer is matched with many lend offers
• The borrower does not pay debt for all lend offers before the deadline and a lender calls createAuctionForCollateral to create an auction for the collateral
• Auction is sold
• The auction buyer, now the current holder of the Receipt token, decides to withdraw the veNFT from the vault by calling veNFTVault::withdraw()
• At this time, both borrower and lenders can not claim liquidation token
• Loss of liquidation for both lenders and borrower

## Proof of Concept
Update the test testDefaultAndAuctionCall in file test/fork/Loan/ltv/OracleOneLenderLoanReceipt.t.sol as below:
```solidity
function testDefaultAndAuctionCall() public {
    MatchOffers();
    uint256[] memory indexes = allDynamicData.getDynamicUintArray(1);
    indexes[0] = 0;
    vm.warp(block.timestamp + 8640010);
    DebitaV3LoanContract.createAuctionForCollateral(0);
    DutchAuction_veNFT auction = DutchAuction_veNFT(DebitaV3LoanContract.getAuctionData().auctionAddress);
    DutchAuction_veNFT.dutchAuction_INFO memory auctionData = auction.getAuctionData();
    vm.warp(block.timestamp + (86400 * 10) + 1);
    address buyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
    deal(AERO, buyer, 100e18);
    vm.startPrank(buyer);
    AEROContract.approve(address(auction), 100e18);
    auction.buyNFT();
    vm.stopPrank();
    address ownerOfNFT = receiptContract.ownerOf(receiptID);
    // buyer withdraws NFT
    vm.startPrank(ownerOfNFT);
    address vaultAddress = receiptContract.s_ReceiptID_to_Vault(receiptID);
    veNFTVault vault = veNFTVault(vaultAddress);
    vault.withdraw();
    // lender claim liquidation token
    vm.stopPrank();
    vm.expectRevert();
    DebitaV3LoanContract.claimCollateralAsLender(0);
}
```
Run the test and console shows:
Ran 1 test for test/fork/Loan/ltv/OracleOneLenderLoanReceipt.t.sol:DebitaAggregatorTest
[PASS] testDefaultAndAuctionCall() (gas: 3381044)

## Recommendation
1/ Update the function getDataByReceipt() to handle the case of non-existent token, instead of reverting.
2/ OR update the logic to fetch the decimals in functions claimCollateralAsNFTLender and claimCollateralNFTAsBorrower.
