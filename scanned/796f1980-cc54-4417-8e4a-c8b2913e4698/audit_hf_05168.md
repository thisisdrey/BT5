# [M] There is a logical error in the re-

## Summary
Severity: Medium
Contest weight: 0.6855
Dataset id: 23236
Source: https://huggingface.co/datasets/Zaevlad/audit-findings-dataset
Type: audit-finding

## Details
There is a logical error in the removeFeeExemption() function, causing feeOverrides[_beneficiary] to never be removed.
```solidity
function removeFeeExemption(address _beneficiary) public onlyOwner {
    // Check that a beneficiary is currently enabled
    uint24 hasExemption = uint24(feeOverrides[_beneficiary] & 0xFFFFFF);
    if (hasExemption != 1) {
        revert NoBeneficiaryExemption(_beneficiary);
    }
    delete feeOverrides[_beneficiary];
    emit BeneficiaryFeeRemoved(_beneficiary);
}
```
Through setFeeExemption(), we know that the lower 24 bits of feeOverrides[_beneficiary] are set to 0xFFFFFF. Therefore, the expression uint24 hasExemption = uint24(feeOverrides[_beneficiary] & 0xFFFFFF) results in 0xFFFFFF & 0xFFFFFF = 0xFFFFFF.
As a result, hasExemption will never be 1, causing removeFeeExemption() to always revert.
Consequently, feeOverrides[_beneficiary] will never be removed.
```solidity
function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
    // Ensure that our custom fee conforms to Uniswap V4 requirements
    if (!_flatFee.isValid()) {
        revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
    }
    // We need to be able to detect if the zero value is a flat fee being applied to
    // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
    // we essentially get a boolean flag to show this.
    feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
    emit BeneficiaryFeeSet(_beneficiary, _flatFee);
}
```
Since feeOverrides[_beneficiary] cannot be removed, the user continues to receive reduced fee benefits, leading to partial fee losses for LPs and the protocol.

## Recommendation
```solidity
function removeFeeExemption(address _beneficiary) public onlyOwner {
    // Check that a beneficiary is currently enabled
    uint24 hasExemption = uint24(feeOverrides[_beneficiary] & 0xFFFFFF);
    if (hasExemption != 0xFFFFFF) {
        revert NoBeneficiaryExemption(_beneficiary);
    }
    delete feeOverrides[_beneficiary];
    emit BeneficiaryFeeRemoved(_beneficiary);
}
```
